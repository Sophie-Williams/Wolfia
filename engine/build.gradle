import info.solidsoft.gradle.pitest.PitestTask

apply plugin: "org.jetbrains.kotlin.jvm"
apply plugin: "com.adarshr.test-logger"
apply plugin: "info.solidsoft.pitest"


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit5:$kotlinVersion"
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
}

test {
    useJUnitPlatform()
}

testlogger {
    theme 'mocha'
}

pitest {
    pitestVersion = "$pitestVersion"
    testPlugin = 'junit5'
    timestampedReports = false
    threads = 4
    outputFormats = ['HTML']
    //like NEW_DEFAULTS but without the ridiculous return value mutators
    mutators = [
            'INVERT_NEGS',
            'MATH',
            'VOID_METHOD_CALLS',
            'NEGATE_CONDITIONALS',
            'CONDITIONALS_BOUNDARY',
            'INCREMENTS',
    ]
}

tasks.withType(PitestTask) {
    doLast {
        exec {
            commandLine 'open', 'build/reports/pitest/index.html'
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "$kotlinJvmTarget"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "$kotlinJvmTarget"
    }
}
